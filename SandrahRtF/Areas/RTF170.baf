IF
Global("Drilling","RTF170",0)
THEN
RESPONSE #100
SetGlobal("Drilling","RTF170",1)
TriggerActivation("ToSanctum",FALSE)
END

IF
Global("Drilling","RTF170",1)
NumItems("Misc7z","Fuel",3)
Contains("Misc7z","Fuel")
THEN
RESPONSE #100
SetGlobal("Drilling","RTF170",2)
END

IF
Global("Drilling","RTF170",5)
Delay(8)
THEN
RESPONSE #100
SetGlobal("Drilling","RTF170",6)
TriggerActivation("ToSanctum",TRUE)
StartCutSceneMode()
ActionOverride(Player1,DisplayStringHead(Myself,@1974))
Wait(8)
ActionOverride(Player1,JumpToPoint([1007.576]))
MoveViewPoint([1007.576],INSTANT)
Explore()
Wait(2)
CreateVisualEffect("spcallli",[1104.662])
CreateVisualEffect("spattck1",[1104.662])
Wait(2)
CreateVisualEffect("am3016n",[974.542])
CreateVisualEffect("spcallli",[974.542])
Wait(2)
CreateVisualEffect("spcallli",[1084.544])
CreateVisualEffect("am3016n",[1084.544])
Wait(2)
CreateVisualEffect("am3016n",[1094.591])
CreateVisualEffect("am3016n",[1094.591])
Wait(4)
FadeToColor([30.0],0)
ActionOverride(Player1,LeaveAreaLUA("RTF171","",[1200.1731],4))
ActionOverride(Player2,LeaveAreaLUA("RTF171","",[1200.1731],7))
ActionOverride(Player3,LeaveAreaLUA("RTF171","",[1200.1731],11))
ActionOverride(Player4,LeaveAreaLUA("RTF171","",[1200.1731],0))
ActionOverride(Player5,LeaveAreaLUA("RTF171","",[1200.1731],8))
ActionOverride(Player6,LeaveAreaLUA("RTF171","",[1200.1731],5))
FadeFromColor([30.0],0)
Wait(2)
EndCutSceneMode()
END

IF
InParty("CVSandr")
Global("PelligFollow","GLOBAL",1)
!InActiveArea("pellig")
!See("pellig")
!Dead("pellig")
THEN
RESPONSE #100
MoveGlobalObject("Pellig","CVSandr")
END

IF
!Allegiance("Pellig",CONTROLLED)
Delay(10)
THEN
RESPONSE #100
ChangeEnemyAlly("Pellig",CONTROLLED)
END

IF
Delay(6)
Global("HaiassJumpOn","GLOBAL",1)
Global("HaiassSeUne","GLOBAL",1)
!InActiveArea("haiass")
!See("haiass")
!Dead("haiass")
THEN
RESPONSE #100
MoveGlobalObject("haiass",Protagonist)
ActionOverride("Haiass",RunAwayFrom(Player1,5))
END

IF
!Allegiance("haiass",CONTROLLED)
Delay(12)
THEN
RESPONSE #100
ChangeEnemyAlly("haiass",CONTROLLED)
END