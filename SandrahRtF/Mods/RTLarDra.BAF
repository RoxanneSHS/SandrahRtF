IF
See([PC])
NumTimesTalkedTo(0)
AreaCheck("RTF151")
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
Allegiance(Myself,ENEMY)
OR(7)
Gender(NearestEnemyOf(Myself),ILLUSIONARY)
StateCheck(NearestEnemyOf(Myself),STATE_INVISIBLE)
StateCheck(NearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
StateCheck(SecondNearestEnemyOf(Myself),STATE_INVISIBLE)
StateCheck(SecondNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
StateCheck(ThirdNearestEnemyOf(Myself),STATE_INVISIBLE)
StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMPROVEDINVISIBILITY)
!GlobalTimerNotExpired("RTDragonTrueSight","LOCALS")
THEN
RESPONSE #100
SetGlobalTimer("RTDragonTrueSight","LOCALS",TWELVE_MINUTES)
ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)
END

IF
!Detect([PC])
HPLT(Myself,50)
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("RTDragonFear","LOCALS",0)
SetGlobal("RTDragonWingBuffet","LOCALS",0)
SetGlobal("RTDragonSilence","LOCALS",0)
SetGlobal("RTDragonBreath","LOCALS",0)
ForceSpell(Myself,CLERIC_HEAL)
END

IF
Global("RTDragonSilence","LOCALS",0)
OR(2)
See(NearestEnemyOfType([0.0.0.LONG_BOW]))
See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
THEN
RESPONSE #100
ForceSpell(LastSeenBy(Myself),DRAGON_SILENCE)
SetGlobal("RTDragonSilence","LOCALS",1)
AttackOneRound(LastSeenBy(Myself))
AttackOneRound(LastSeenBy(Myself))
END

IF
OR(3)
See(NearestEnemyOfType([PC.0.0.BARD_ALL]))
See(NearestEnemyOfType([PC.0.0.LONG_BOW]))
See(NearestEnemyOfType([PC.0.0.SORCERER]))
!GlobalTimerNotExpired("RTDragonSpellTrigger","LOCALS")
THEN
RESPONSE #100
SetGlobalTimer("RTDragonSpellTrigger","LOCALS",1000)
ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
ReallyForceSpell(Myself,BEHOLDER_SPELL_TURNING)
END

IF
Detect(NearestEnemyOf(Myself))
HPPercentLT(Myself,100)
See(LastAttackerOf(Myself))
!GlobalTimerNotExpired("RTDragonSpSequencer","LOCALS")
THEN
RESPONSE #100
SetGlobalTimer("RTDragonSpSequencer","LOCALS",1000)
SetGlobalTimer("RTDragonDispel","LOCALS",SIX_MINUTES)
ReallyForceSpell(Myself,DRAGON_STONE_SKIN)
ReallyForceSpell(Myself,CLERIC_DEATH_WARD)
NoAction()
ReallyForceSpell(Myself,WIZARD_HASTE)
ReallyForceSpell(LastAttackerOf(Myself),WIZARD_TRUE_DISPEL_MAGIC)
END

IF
Allegiance(Myself,ENEMY)
!CheckStatGT(Myself,0,STONESKINS)
!GlobalTimerNotExpired("RTDragonStoneSkin","LOCALS")
RandomNum(2,1)
THEN
RESPONSE #100
SetGlobalTimer("RTDragonStoneSkin","LOCALS",SIX_MINUTES)
ForceSpell(Myself,DRAGON_STONE_SKIN)
END

IF
OR(3)
SpellCast([GOODCUTOFF],WIZARD_CLOUDKILL)
SpellCast([GOODCUTOFF],WIZARD_INCENDIARY_CLOUD)
SpellCast([GOODCUTOFF],WIZARD_DEATH_FOG)
!GlobalTimerNotExpired("RTDragonZone","LOCALS")
THEN
RESPONSE #100
SetGlobalTimer("RTDragonZone","LOCALS",TWO_ROUNDS)
ForceSpell(NearestEnemyOf(Myself),DRAGON_WING_BUFFET)
END

IF
See(NearestEnemyOf(Myself))
OR(3)
Class(LastSeenBy(Myself),BARD_ALL)
Class(LastSeenBy(Myself),LONG_BOW)
Class(LastSeenBy(Myself),SORCERER)
OR(2)
HasBounceEffects(LastSeenBy(Myself))
HasImmunityEffects(LastSeenBy(Myself))
!GlobalTimerNotExpired("RTDragonRemoveMagic","LOCALS")
!StateCheck(Myself,STATE_INVISIBLE)
Allegiance(Myself,ENEMY)
THEN
RESPONSE #34
SetGlobalTimer("RTDragonRemoveMagic","LOCALS",EIGHT_ROUNDS)
ForceSpell(LastSeenBy(Myself),WIZARD_SECRET_WORD)
ForceSpell(LastSeenBy(Myself),WIZARD_BREACH)ForceSpell(LastSeenBy(Myself),WIZARD_PIERCE_MAGIC)
Continue()
RESPONSE #33
SetGlobalTimer("RTDragonRemoveMagic","LOCALS",EIGHT_ROUNDS)
ForceSpell(LastSeenBy(Myself),WIZARD_BREACH)
ForceSpell(LastSeenBy(Myself),WIZARD_PIERCE_MAGIC)
Continue()
RESPONSE #33
SetGlobalTimer("RTDragonRemoveMagic","LOCALS",EIGHT_ROUNDS)
ForceSpell(LastSeenBy(Myself),WIZARD_PIERCE_MAGIC)
Continue()
END

IF
See(NearestEnemyOf(Myself))
!HasBounceEffects(LastSeenBy(Myself))
!CheckStatGT(LastSeenBy(Myself),60,RESISTMAGIC)
!CheckStatLT(LastSeenBy(Myself),4,SAVEVSSPELL)
!GlobalTimerNotExpired("RTDragonMagicalBarrage","LOCALS")
!StateCheck(Myself,STATE_INVISIBLE)
THEN
RESPONSE #25
SetGlobalTimer("RTDragonMagicalBarrage","LOCALS",THREE_ROUNDS)
ForceSpell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
RESPONSE #25
SetGlobalTimer("RTDragonMagicalBarrage","LOCALS",THREE_ROUNDS)
ForceSpell(LastSeenBy(Myself),WIZARD_HOLD_PERSON)
RESPONSE #25
SetGlobalTimer("RTDragonMagicalBarrage","LOCALS",THREE_ROUNDS)
ForceSpell(LastSeenBy(Myself),WIZARD_CHROMATIC_ORB)
RESPONSE #25
SetGlobalTimer("RTDragonMagicalBarrage","LOCALS",THREE_ROUNDS)
ForceSpell(LastSeenBy(Myself),WIZARD_SLOW)
END

IF
Global("RTDragonBreath","LOCALS",0)
THEN
RESPONSE #100
SetGlobalTimer("RTDragonBreathWeapon","LOCALS",SIX_MINUTES)
SetGlobal("RTDragonBreath","LOCALS",1)
END

IF
See(NearestEnemyOf(Myself))
Global("RTDragonBreathSequence","LOCALS",0)
THEN
RESPONSE #100
SetGlobalTimer("RTDragonBreathSequenceTimer","LOCALS",TWO_MINUTES)
ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_FEAR)
ReallyForceSpell(NearestEnemyOf(Myself),DRAGON_LOWER_FIRE)
ForceSpell(LastSeenBy(Myself),DRAGON_WING_BUFFET)
SetGlobal("RTDragonBreathSequence","LOCALS",1)
END

IF
See(NearestEnemyOf(Myself))
Global("RTDragonBreathSequence","LOCALS",1)
GlobalTimerExpired("RTDragonBreathSequenceTimer","LOCALS")
THEN
RESPONSE #100
ForceSpell(LastSeenBy(Myself),DRAGON_WING_BUFFET)
Wait(1)
SetGlobal("RTDragonBreathSequence","LOCALS",2)
END

IF
See(NearestEnemyOf(Myself))
Global("RTDragonBreathSequence","LOCALS",2)
HasItem("CBDRGDP1",Myself)
THEN
RESPONSE #100
SetGlobalTimer("RTDragonBreathSequenceTimer","LOCALS",FOUR_MINUTES)
SetGlobal("RTDragonBreathSequence","LOCALS",3)
UseItem("CBDRGDP1",LastSeenBy(Myself))
END

IF
See(SecondNearestEnemyOf(Myself))
GlobalTimerExpired("RTDragonBreathSequenceTimer","LOCALS")
Global("RTDragonBreathSequence","LOCALS",3)
HasItem("CBDRGDP1",Myself)
THEN
RESPONSE #100
SetGlobalTimer("RTDragonBreathSequenceTimer","LOCALS",NINE_MINUTES)
SetGlobal("RTDragonBreathSequence","LOCALS",4)
UseItem("CBDRGDP1",SecondNearestEnemyOf(Myself))
END

IF
See(NearestEnemyOf(Myself))
Global("RTDragonBreathSequence","LOCALS",4)
GlobalTimerExpired("RTDragonBreathSequenceTimer","LOCALS")
THEN
RESPONSE #100
SetGlobalTimer("RTDragonBreathSequenceTimer","LOCALS",TWELVE_MINUTES)
ForceSpell(LastSeenBy(Myself),SILVER_DRAGON_BREATH_COLD)
SetGlobal("RTDragonBreathSequence","LOCALS",5)
END

IF
GlobalTimerExpired("RTDragonBreathSequenceTimer","LOCALS")
Global("RTDragonBreathSequence","LOCALS",5)
THEN
RESPONSE #100
SetGlobal("RTDragonBreathSequence","LOCALS",0)
END

IF
!See(NearestEnemyOf(Myself))
THEN
RESPONSE #100
SetGlobal("RTDragonBreathSequence","LOCALS",0)
END


