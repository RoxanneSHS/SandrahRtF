IF
Detect([PC])
Range(LastSeenBy(Myself),20)
NumTimesTalkedTo(0)
THEN
RESPONSE #100
StartDialogueNoSet(LastSeenBy(Myself))
END

IF
Allegiance(Myself,ENEMY)
!GlobalTimerNotExpired("prep","LOCALS")
THEN
RESPONSE #100
SetGlobalTimer("prep","LOCALS",TWELVE_MINUTES)
ApplySpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
Continue()
END

IF
See(NearestEnemyOf(Myself))
RandomNum(4,1)
NumCreatureLT([ENEMY.0.WRAITH],2)
Global("storm","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("storm","LOCALS",1)
ForceSpell(NearestEnemyOf(Myself),WIZARD_ICE_STORM)
Continue()
END

IF
See(NearestEnemyOf(Myself))
RandomNum(3,1)
NumCreatureLT([ENEMY.0.WRAITH],2)
!GlobalTimerNotExpired("cone","LOCALS")
THEN
RESPONSE #100
SetGlobalTimer("cone","LOCALS",8)
ForceSpell(NearestEnemyOf(Myself),WIZARD_CONE_OF_COLD)
Continue()
END

IF
DamageTakenGT(10)
See(LastAttackerOf(Myself))
THEN
RESPONSE #100
AttackReevaluate(LastAttackerOf(Myself),45)
END

IF
See(NearestEnemyOf(Myself))
!See(SecondNearestEnemyOf(Myself))
THEN
RESPONSE #100
AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
See(NearestEnemyOf(Myself))
See(SecondNearestEnemyOf(Myself))
!See(ThirdNearestEnemyOf(Myself))
THEN
RESPONSE #50
AttackReevaluate(NearestEnemyOf(Myself),30)
RESPONSE #100
AttackReevaluate(SecondNearestEnemyOf(Myself),30)
END

IF
See(NearestEnemyOf(Myself))
See(SecondNearestEnemyOf(Myself))
See(ThirdNearestEnemyOf(Myself))
THEN
RESPONSE #50
AttackReevaluate(NearestEnemyOf(Myself),30)
RESPONSE #50
AttackReevaluate(SecondNearestEnemyOf(Myself),30)
RESPONSE #50
AttackReevaluate(ThirdNearestEnemyOf(Myself),30)
END


