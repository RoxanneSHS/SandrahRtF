IF
Global("LouredB","GLOBAL",0)
AreaType(OUTDOOR)
!AreaType(CITY)
See("CVSandr")
CombatCounter(0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("LouredB","GLOBAL",1)
END

IF
GlobalGT("LouredB","GLOBAL",0)
InParty("RTMeli")
RealGlobalTimerExpired("LoureedT","LOCALS")
Global("LouredM","GLOBAL",0)
!AreaType(DUNGEON)
!AreaCheck("rtf005")
CombatCounter(0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("LouredM","GLOBAL",1)
END

IF
GlobalGT("LouredB","GLOBAL",0)
InParty("CVGiran")
RealGlobalTimerExpired("LoureedT","LOCALS")
Global("LouredG","GLOBAL",0)
HPPercentLT("CVGiran",90)
CombatCounter(0)
Delay(12)
THEN
RESPONSE #100
SetGlobal("LouredG","GLOBAL",1)
END

IF
Global("LouredB","GLOBAL",2)
RealGlobalTimerExpired("LoureedT","LOCALS")
!AreaType(DUNGEON)
See("CVSandr")
CombatCounter(0)
Delay(12)
THEN
RESPONSE #100
IncrementGlobal("LouredB","GLOBAL",1)
END

IF
Global("LouredB","GLOBAL",4)
OR(2)
Global("SanRTFPlot1","GLOBAL",5)
PartyHasItem("RTVelsym")
THEN
RESPONSE #100
IncrementGlobal("LouredB","GLOBAL",1)
END

IF
Global("LouredB","GLOBAL",6)
RealGlobalTimerExpired("LoureedT","LOCALS")
GlobalGT("SanRTFPlot2","GLOBAL",4)
OR(4)
AreaCheck("ar6600")
AreaCheck("cvsh12")
AreaCheck("cvsh10")
AreaType(FOREST)
See("CVSandr")
CombatCounter(0)
Delay(12)
THEN
RESPONSE #100
IncrementGlobal("LouredB","GLOBAL",1)
END

IF
Global("Loureed900","LOCALS",0)  
AreaCheck("ar0900")
Delay(6) 
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("Loureed900","LOCALS",1)
END

IF
Global("Loureed900","LOCALS",2)
RealGlobalTimerExpired("LoureedT","LOCALS")
OR(11)
AreaCheck("ar7232")
AreaCheck("rtf053")
AreaCheck("ar7102")
AreaCheck("ar7249")
AreaCheck("ar7609")
AreaCheck("CVelm4")
AreaCheck("ar3702")
AreaCheck("ar6804")
AreaCheck("rtf011")
AreaCheck("rtf007")
AreaCheck("rtf095")
Delay(6) 
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("Loureed900","LOCALS",3)
END

IF
Global("LouredB","GLOBAL",8)
RealGlobalTimerExpired("LoureedT","LOCALS")
OR(3)
Global("LouredM","GLOBAL",2)
Global("SanRomPath","GLOBAL",1)
Global("LouredG","GLOBAL",2)
OR(3)
InParty("RTMeli")
InParty("CVGiran")
Global("SanRomPath","GLOBAL",1)
See("CVSandr")
CombatCounter(0)
!AreaType(DUNGEON)
!AreaType(OUTDOOR)
THEN
RESPONSE #100
SetGlobal("LouredB","GLOBAL",9)
END

IF
Global("RTLouPlot","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("RTLouPlot","GLOBAL",2)
CreateCreatureObjectDoor("RTLoufav",Myself,0,0,0)
END

IF
Global("RTLouPlot","GLOBAL",5)
THEN
RESPONSE #100
SetGlobal("RTLouPlot","GLOBAL",6)
RealSetGlobalTimer("LoureedT","LOCALS",70)
END

IF
Global("RTLouPlot","GLOBAL",6)
RealGlobalTimerExpired("LoureedT","LOCALS")
THEN
RESPONSE #100
SetGlobal("RTLouPlot","GLOBAL",7)
END

IF
Global("RTLouMiss","LOCALS",0)
AreaCheck("RTF130")
Global("SanRTFPlot4","GLOBAL",5)
InParty("Pellig3")
See("Pellig3")
THEN
RESPONSE #100
SetGlobal("RTLouMiss","LOCALS",1)
END

IF
Global("RTLouPlot","GLOBAL",8)
GlobalGT("SanRTFPlot4","GLOBAL",6)
AreaType(FOREST)
THEN
RESPONSE #100
SetGlobal("RTLouPlot","GLOBAL",9)
END

IF
OR(8)
Global("LouredB","GLOBAL",1)
Global("LouredB","GLOBAL",3)
Global("LouredB","GLOBAL",5)
Global("LouredB","GLOBAL",7)
Global("LouredB","GLOBAL",9)
Global("LouredG","GLOBAL",1)
Global("LouredM","GLOBAL",1)
Global("RTLouPlot","GLOBAL",7)
!See([ENEMY])
See("CVSandr")
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact(Player1)
END

IF
OR(4)
Global("Loureed900","LOCALS",1)
Global("Loureed900","LOCALS",3)
Global("RTLouPlot","GLOBAL",9)
Global("RTLouMiss","LOCALS",1)
!See([ENEMY])
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogNoSet("Loureed")
END

IF
AttackedBy([ANYONE],DEFAULT)
THEN
RESPONSE #100
EquipMostDamagingMelee()
AttackReevaluate(LastAttackerOf(Myself),10)
END

IF
See(NearestEnemyOf(Myself))
Range(NearestEnemyOf(Myself),10)
THEN
RESPONSE #100
EquipMostDamagingMelee()
AttackReevaluate(NearestEnemyOf(Myself),20)
END

IF
See(NearestEnemyOf(Myself))
!Range(LastSeenBy(Myself),30)
THEN
RESPONSE #100
MoveToObject(LastSeenBy(Myself))
END

IF
See(NearestEnemyOf(Myself))
!Range(NearestEnemyOf(Myself),10)
THEN
RESPONSE #100
EquipRanged()
AttackReevaluate(NearestEnemyOf(Myself),20)
END

IF
OR(4)
StateCheck(Myself,STATE_PANIC)
SpellCast([ANYONE],WIZARD_HORROR)
SpellCast([ANYONE],WIZARD_SPOOK)
SpellCastPriest([ANYONE],CLERIC_CLOAK_OF_FEAR)
HaveSpell(CLERIC_REMOVE_FEAR)
See(NearestEnemyOf(Myself))
THEN
RESPONSE #100
Spell(Myself,CLERIC_REMOVE_FEAR)
END

IF
HPPercentLT(Myself,30)
HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
THEN
RESPONSE #100
Spell(Myself,CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
HPPercentLT(Myself,60)
HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
THEN
RESPONSE #100
Spell(Myself,CLERIC_CURE_LIGHT_WOUNDS)
END