IF
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaCheck("RTF068")
OR(5)
Global("RTGiranQuest","GLOBAL",1)
Global("RTGiranQuest","GLOBAL",5)
Global("RTGiranQuest","GLOBAL",7)
Global("RTGiranQuest","GLOBAL",9)
Global("RTGiranQuest","GLOBAL",11)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogNoSet("CVGiran")
END

IF
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
!AreaCheck("RTF068")
OR(3)
Global("RTGiranNash","BG3300",1)
Global("GhotGir","LOCALS",1)
Global("PellGir","LOCALS",1)
THEN
RESPONSE #100
Interact([PC])
END

IF
Global("RTGiranQuest","GLOBAL",12)
See("RTHartl")
THEN
RESPONSE #100
ClearAllActions()
StartCutSceneMode()
EquipMostDamagingMelee()
MoveToObject("RTHartl")
ActionOverride("RTHartl",StartDialogueNoSet([PC])
END

IF
Global("SanRTFPlot4","GLOBAL",5)
InParty("CVIzzy")
InParty("Pellig3")
AreaType(CITY)
CombatCounter(0)
Global("PellGir","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("PellGir","LOCALS",1)
END

IF
AreaCheck("ar10pb")
CombatCounter(0)
!See([ENEMY])
Global("GhotGir","LOCALS",0)
OR(6)
See("Infohouse")
See("Door12pb")
See("Door11pb")
See("Wagon5")
See("Chest1")
See("Chest6")
THEN
RESPONSE #100
SetGlobal("GhotGir","LOCALS",1)
END

IF
Global("RTGiranNash","BG3300",0)
AreaCheck("BG3300")
InParty(Myself)
THEN
RESPONSE #100
SetGlobal("RTGiranNash","BG3300",1)
END

IF
HPPercentLT(Myself,50)
See(NearestEnemyOf(Myself))
HPGT(LastSeenBy(Myself),50)
HaveSpell(MINSC_BERSERK)
THEN
RESPONSE #100
Spell(Myself,MINSC_BERSERK)
END

IF
AttackedBy([ANYONE],DEFAULT)
THEN
RESPONSE #100
EquipMostDamagingMelee()
AttackReevaluate(LastAttackerOf(Myself),10)
END

IF
See(NearestEnemyOf(Myself))
Range(NearestEnemyOf(Myself),10)
THEN
RESPONSE #100
EquipMostDamagingMelee()
AttackReevaluate(NearestEnemyOf(Myself),20)
END

IF
See(NearestEnemyOf(Myself))
!Range(LastSeenBy(Myself),30)
THEN
RESPONSE #100
MoveToObject(LastSeenBy(Myself))
END

IF
See(NearestEnemyOf(Myself))
!Range(NearestEnemyOf(Myself),10)
THEN
RESPONSE #100
EquipRanged()
AttackReevaluate(NearestEnemyOf(Myself),20)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(CLERIC_ARMOR_OF_FAITH)
CheckStatGT(Myself,3,ARMORCLASS)
HPGT(LastSeenBy(Myself),20)
THEN
RESPONSE #100
Spell(Myself,CLERIC_ARMOR_OF_FAITH)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(CLERIC_BLESS)
HPGT(LastSeenBy(Myself),20)
!StateCheck(LastSeenBy(Myself),STATE_BLESS)
THEN
RESPONSE #100
Spell(Myself,CLERIC_BLESS)
END

IF
OR(4)
StateCheck(Myself,STATE_PANIC)
SpellCast([ANYONE],WIZARD_HORROR)
SpellCast([ANYONE],WIZARD_SPOOK)
SpellCastPriest([ANYONE],CLERIC_CLOAK_OF_FEAR)
HaveSpell(CLERIC_REMOVE_FEAR)
See(NearestEnemyOf(Myself))
THEN
RESPONSE #100
Spell(Myself,CLERIC_REMOVE_FEAR)
END

IF
See(NearestEnemyOfType([0.ANIMAL]))
HaveSpell(RANGER_CHARM_ANIMAL)
THEN
RESPONSE #100
Spell(LastSeenBy(Myself),RANGER_CHARM_ANIMAL)
END

IF
See(NearestEnemyOf(Myself))
!Range(LastSeenBy(Myself),20)
HaveSpell(CLERIC_ENTANGLE)
AreaType(OUTDOOR)
THEN
RESPONSE #100
Spell(LastSeenBy(Myself),CLERIC_ENTANGLE)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(CLERIC_DOOM)
THEN
RESPONSE #50
Spell(LastSeenBy(Myself),CLERIC_DOOM)
RESPONSE #50
Continue()
END

IF
HPPercentLT(Myself,30)
HaveSpell(CLERIC_CURE_MEDIUM_WOUNDS)
THEN
RESPONSE #100
Spell(Myself,CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
HPPercentLT(Myself,60)
HaveSpell(CLERIC_CURE_LIGHT_WOUNDS)
THEN
RESPONSE #100
Spell(Myself,CLERIC_CURE_LIGHT_WOUNDS)
END

IF
HasItem("POTN08",Myself)
HPPercentLT(Myself,80)
THEN
RESPONSE #100
UseItem("POTN08",Myself)
END

IF
HasItem("POTN52",Myself)
HPPercentLT(Myself,50)
THEN
RESPONSE #100
UseItem("POTN52",Myself)
END

IF
GlobalGT("RTColan","GLOBAL",9)
AreaCheck("RTF060")
CombatCounter(0)
See("cm2300a1")
Global("RTGiranQuest","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("RTGiranQuest","GLOBAL",1)
END

IF
Global("RTGiranQuest","GLOBAL",6)
AreaCheck("RTF090")
THEN
RESPONSE #100
SetGlobal("RTGiranQuest","GLOBAL",7)
END

IF
Global("RTGiranQuest","GLOBAL",8)
AreaCheck("RTF073")
OR(2)
See("RSlaver1")
See("RSlaver2")
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("RTGiranQuest","GLOBAL",9)
END
