IF
Global("RTSimbul","RTF143",5)
See(Player1)
Delay(6)
THEN
RESPONSE #100
SetGlobal("RTSimbul","RTF143",6)
StartDialogueNoSet([PC])
END

IF
AreaCheck("RTF140")
Global("CyrChance","RTF140",1)
See("chalcy01")
THEN
RESPONSE #100
ActionOverride("chalcy01",StartDialogueNoSet([PC]))
END

IF
OR(2)
AreaCheck("BG3202")
AreaCheck("BG4802")
OR(2)
Global("RThintNash","BG3202",1)
Global("Thamove","BG4802",1)
See("Thalantyr")
THEN
RESPONSE #100
ActionOverride("Thalantyr",StartDialogueNoSet([PC]))
END

IF
AreaCheck("CVUmo5")
Global("PelItem2","LOCALS",0)
See("CVQidr")
THEN
RESPONSE #100
SetGlobal("PelItem2","LOCALS",1)
StartDialogueNoSet([PC])
END

IF
AreaCheck("RTF018")
OR(2)
Global("PelItem3","LOCALS",1)
Global("PelItem3","LOCALS",2)
THEN
RESPONSE #100
SetGlobal("PelItem3","LOCALS",3)
ClearAllActions()
StartCutSceneMode()
MoveToObject("SanMyst")
EndCutSceneMode()
StartDialogueNoSet([PC])
END

IF
Global("PelItem3","LOCALS",4)
AreaCheck("RTF026")
See("SanMyst")
THEN
RESPONSE #100
SetGlobal("PelItem3","LOCALS",5)
ClearAllActions()
StartCutSceneMode()
MoveToObject("SanMyst")
EndCutSceneMode()
StartDialogueNoSet([PC])
END

IF
Global("Thamove","BG4802",2)
Global("PelItem1","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("PelItem1","LOCALS",1)
END

IF
Global("PelItem1","LOCALS",1)
AreaCheck("BG4800")
Global("RTPriQ","BG4800",0)
THEN
RESPONSE #100
SetGlobal("RTPriQ","BG4800",1)
END

IF
Global("PelItem1","LOCALS",1)
AreaCheck("BG3201")
Global("RTItem","BG3201",0)
THEN
RESPONSE #100
SetGlobal("RTItem","BG3201",1)
END

IF
Global("PelItem1","LOCALS",2)
AreaCheck("BG3200")
Global("RTHunt","BG3200",0)
THEN
RESPONSE #100
SetGlobal("RTHunt","BG3200",1)
END

IF
Global("PelItem1","LOCALS",1)
PartyHasItem("RTSymmy")
THEN
RESPONSE #100
SetGlobal("PelItem1","LOCALS",2)
Continue()
END

IF
Global("PelItem2","LOCALS",2)
PartyHasItem("RTBummy")
THEN
RESPONSE #100
SetGlobal("PelItem2","LOCALS",3)
Continue()
END

IF
Global("PelItem2","LOCALS",3)
Global("PelItem1","LOCALS",2)
Global("PelItem3","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("PelItem3","LOCALS",1)
Continue()
END

IF
Global("PelItem3","LOCALS",4)
AreaCheck("RTF019")
Global("RTFOn","RTF019",1)
THEN
RESPONSE #100
SetGlobal("RTFOn","RTF019",2)
Continue()
END

IF
Global("SanRTFPlot4","GLOBAL",5)
InParty("RTMeli")
OR(2)
AreaCheck("CVUMO2")
AreaCheck("CVUMO4")
See("RTMeli")
CombatCounter(0)
Global("PellMellB","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("PellMellB","LOCALS",1)
StartDialogueNoSet([PC])
END

IF
!HasItem("Simbul",Myself)
!AreaCheck("RTF026")
OR(6)
HasItem("Simbul",Player1)
HasItem("Simbul",Player2)
HasItem("Simbul",Player3)
HasItem("Simbul",Player4)
HasItem("Simbul",Player5)
HasItem("Simbul",Player6)
THEN
RESPONSE #100
ActionOverride(Player1,GiveItem("Simbul","Pellig3"))
ActionOverride(Player2,GiveItem("Simbul","Pellig3"))
ActionOverride(Player3,GiveItem("Simbul","Pellig3"))
ActionOverride(Player4,GiveItem("Simbul","Pellig3"))
ActionOverride(Player5,GiveItem("Simbul","Pellig3"))
ActionOverride(Player6,GiveItem("Simbul","Pellig3"))
DisplayStringHead(Myself,@1930)
END

IF
!HasItem("Simbul",Myself)
!AreaCheck("RTF026")
!HasItem("Simbul",Player1)
!HasItem("Simbul",Player2)
!HasItem("Simbul",Player3)
!HasItem("Simbul",Player4)
!HasItem("Simbul",Player5)
!HasItem("Simbul",Player6)
THEN
RESPONSE #100
ActionOverride(Player1,PickUpItem("Simbul"))
ActionOverride(Player2,PickUpItem("Simbul"))
ActionOverride(Player3,PickUpItem("Simbul"))
ActionOverride(Player4,PickUpItem("Simbul"))
ActionOverride(Player5,PickUpItem("Simbul"))
ActionOverride(Player6,PickUpItem("Simbul"))
DisplayStringHead(Myself,@1931)
END

IF
HPLT(Myself,35)
THEN
RESPONSE #100
ReallyForceSpell(Myself,CLERIC_HEAL)
Continue()
END